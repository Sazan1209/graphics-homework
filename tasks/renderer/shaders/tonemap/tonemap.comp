#version 430
#extension GL_GOOGLE_include_directive : require

#include "tonemap.glsl"
#include "../utils.glsl"
#include "../fxaa_defines.glsl"

layout(local_size_x = 32, local_size_y = 32) in;

layout(push_constant) uniform terraintest_pc{
  bool forceLinear;
  bool useFxaa;
};

layout(binding = 0, rgba8) restrict uniform image2D image;
layout(binding = 1, r32f) restrict readonly uniform image2D lums;
layout(binding = 2, std430) buffer A
{
  float minLum;
  float maxLum;
  float cumDist[bucketCount];
};

void main()
{
  const ivec2 size = imageSize(image);
  if (!all(greaterThan(size, gl_GlobalInvocationID.xy)))
  {
    return;
  }

  vec3 pixel = toLinear(imageLoad(image, ivec2(gl_GlobalInvocationID.xy)).xyz);
  const float lum = imageLoad(lums, ivec2(gl_GlobalInvocationID.xy)).x;

  // linear interpolation
  if (forceLinear || maxLum / minLum <= maxScreenLum / minScreenLum)
  {
    pixel = pixel * (lum / maxLum);
  }
  else
  {
    const float minB = log(minLum);
    const float maxB = log(maxLum);
    const float dB = (maxB - minB) / bucketCount;
    const float b = log(lum);
    const uint ind = min(uint(floor((b - minB) / dB)), bucketCount - 1);

    const float total = cumDist[bucketCount - 1];
    const float distrPrev = ind == 0 ? 0.0 : float(cumDist[ind - 1]) / total;
    const float distr = float(cumDist[ind]) / total;

    const float coef = (b - minB) / dB - float(ind);
    const float diff = mix(distrPrev, distr, coef);

    const float resB = mix(minScreenB, maxScreenB, diff);
    pixel = pixel * exp(resB);
  }

  vec3 colorSRGB = fromLinear(pixel);
#if (FXAA_GREEN_AS_LUMA == 0)
  float fxaaLum = useFxaa ? dot(colorSRGB, vec3(0.299, 0.587, 0.114)) : 1.0;
  imageStore(image, ivec2(gl_GlobalInvocationID.xy), vec4(colorSRGB, fxaaLum));
#else
  imageStore(image, ivec2(gl_GlobalInvocationID.xy), vec4(colorSRGB, 1.0));
#endif
}
