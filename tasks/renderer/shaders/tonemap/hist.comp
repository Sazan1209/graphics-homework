#version 430
#extension GL_GOOGLE_include_directive : require

#include "tonemap.glsl"

layout(local_size_x = 32, local_size_y = 32) in;

// needed to decrease the amount of between group syncing
shared uint localBuckets[bucketCount];

layout(binding = 0, r11f_g11f_b10f) restrict readonly uniform image2D image;
layout(binding = 1, std430) buffer A
{
  float minLum;
  float maxLum;
  uint outputBuf[bucketCount];
};

uint bucketFromLum(float lum)
{
  const float minB = log(minLum);
  const float maxB = log(maxLum);
  const float dB = (maxB - minB) / bucketCount;

  const float b = log(lum);
  return min(uint(floor((b - minB) / dB)), bucketCount - 1);
}

void main()
{
  const uvec2 size = imageSize(image);
  if (size.x <= gl_GlobalInvocationID.x || size.y <= gl_GlobalInvocationID.y)
  {
    return;
  }
  const uint ind = gl_LocalInvocationIndex;
  if (ind < bucketCount)
  {
    localBuckets[ind] = 0;
  }
  barrier();
  const vec3 pixel = imageLoad(image, ivec2(gl_GlobalInvocationID.xy)).xyz;
  const float lum = dot(pixel, lumVec);

  atomicAdd(localBuckets[bucketFromLum(lum)], 1);
  barrier();
  memoryBarrierShared();
  if (ind < bucketCount)
  {
    atomicAdd(outputBuf[ind], localBuckets[ind]);
  }
}
