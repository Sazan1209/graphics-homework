#version 450
#extension GL_GOOGLE_include_directive : require


#include "resolve.h"

layout(local_size_x = 32, local_size_y = 32) in;

layout(binding = 0, r11f_g11f_b10f) restrict uniform image2D albedo;
layout(binding = 1, r32f) restrict readonly uniform image2D depths;
layout(binding = 2, r11f_g11f_b10f) restrict uniform image2D normals;

layout(push_constant) uniform pc
{
  mat4 mView;
  float near;
  float far;
  float tanFov;
};

layout(binding = 3, std430) buffer a
{
  PointLight lights[];
};

Sunlight sunlight = Sunlight(vec3(1.0 / 2, -sqrt(3.0) / 2.0, 0), 10, vec3(1, 1, 1), 0.005);

void main()
{
  ivec2 resolution = imageSize(albedo);
  if (any(greaterThan(gl_GlobalInvocationID.xy, resolution)))
  {
    return;
  }
  float depth = imageLoad(depths, ivec2(gl_GlobalInvocationID.xy)).x;
  if (depth == 1.0)
  {
    return;
  }

  float aspect = float(resolution.x) / float(resolution.y);

  vec3 pos;

  vec2 fragCoord = vec2(gl_GlobalInvocationID.xy) * 2.0 / vec2(resolution) - 1.0;
  pos.z = depth * (far - near) + near;
  pos.y = tanFov * pos.z * fragCoord.y;
  pos.x = tanFov * pos.z * aspect * fragCoord.x;
  pos.z = -pos.z;

  vec3 normal = mat3(mView) * imageLoad(normals, ivec2(gl_GlobalInvocationID.xy)).xyz;
  vec3 pixel = imageLoad(albedo, ivec2(gl_GlobalInvocationID.xy)).rgb;

  vec3 light = vec3(0);
  vec3 eyeDir = normalize(-pos);

  for (int i = 0; i < lights.length(); ++i)
  {
    vec3 lightPos = (mView * vec4(lights[i].pos, 1.0)).xyz;
    float lightDist = distance(pos, lightPos);
    vec3 lightDir = normalize(lightPos - pos);
    float attenuation = 1.0 / (1.0 + attenuationCoef * lightDist * lightDist);

    float diffuse = max(0.0, dot(normal, lightDir));
    float specular = 0; // pow(max(0.0, dot(eyeDir, normalize(lightDir + normal))), lightExponent);
    light += lights[i].color * lights[i].strength * (diffuse + specular) * attenuation;
  }

  {
    vec3 lightDir = -mat3(mView) * sunlight.dir;

    float diffuse = max(0.0, dot(normal, lightDir));
    float specular = 0.0; // pow(max(0.0, dot(normal, normalize(lightDir + eyeDir))),
                          // lightExponent);
    light += sunlight.color * sunlight.strength * (diffuse + specular + sunlight.ambient);
  }

  pixel *= light;

  imageStore(albedo, ivec2(gl_GlobalInvocationID.xy), vec4(pixel, 1.0));
  //imageStore(normals, ivec2(gl_GlobalInvocationID.xy), vec4(fragCoord, 1.0, 1.0));
}
