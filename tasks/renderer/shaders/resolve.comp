#version 450
#extension GL_GOOGLE_include_directive : require


#include "resolve.glsl"

layout(local_size_x = 32, local_size_y = 32) in;

layout(binding = 0, r11f_g11f_b10f) restrict uniform image2D albedo;
layout(binding = 1, r32f) restrict readonly uniform image2D depths;
layout(binding = 2, rgba8) restrict uniform image2D normals;

layout(binding = 3, std430) buffer resolve_point_lights
{
  PointLight lights[];
};

layout(binding = 4, std140) readonly restrict uniform resolve_sunlight
{
  Sunlight sunlight;

  vec3 skyColor;
  float lightExponent;

  mat4 mView;

  float near;
  float far;
  float tanFov;
  float attenuationCoef;
};

// Sunlight(vec3(1.0 / 2, -sqrt(3.0) / 2.0, 0), 0.0, vec3(1, 1, 1), 0.05);
// sunlight.strength * vec3(135, 206, 235) / 255.0;
// 1.0
// 0.005

void main()
{
  ivec2 resolution = imageSize(albedo);
  if (any(greaterThan(gl_GlobalInvocationID.xy, resolution)))
  {
    return;
  }
  float depth = imageLoad(depths, ivec2(gl_GlobalInvocationID.xy)).x;
  if (depth == 1.0)
  {
    imageStore(albedo, ivec2(gl_GlobalInvocationID.xy), vec4(skyColor * sunlight.strength, 1.0));
    return;
  }

  float aspect = float(resolution.x) / float(resolution.y);

  vec3 pos;

  vec2 fragCoord = (vec2(gl_GlobalInvocationID.xy) + 0.5) * 2.0 / vec2(resolution) - 1.0;
  pos.z = near / (1.0 - ((far - near) / far) * depth);
  pos.y = -tanFov * pos.z * fragCoord.y;
  pos.x = -tanFov * pos.z * aspect * fragCoord.x;

  vec3 normal = mat3(mView) * imageLoad(normals, ivec2(gl_GlobalInvocationID.xy)).xyz;
  vec3 pixel = imageLoad(albedo, ivec2(gl_GlobalInvocationID.xy)).rgb;

  vec3 light = vec3(0);
  vec3 eyeDir = normalize(-pos);

  for (int i = 0; i < lights.length(); ++i)
  {
    vec3 lightPos = (mView * vec4(lights[i].pos, 1.0)).xyz;
    float lightDist = distance(pos, lightPos);
    vec3 lightDir = normalize(lightPos - pos);
    float attenuation = 1.0 / (1.0 + attenuationCoef * lightDist * lightDist);

    float diffuse = max(0.0, dot(normal, lightDir));
    float specular = pow(max(0.0, dot(normal, normalize(lightDir + eyeDir))), lightExponent);
    light += lights[i].color * lights[i].strength * (diffuse + specular) * attenuation;
  }

  {
    vec3 lightDir = -mat3(mView) * sunlight.dir;

    float diffuse = max(0.0, dot(normal, lightDir));
    float specular = pow(max(0.0, dot(normal, normalize(lightDir + eyeDir))), lightExponent);
    light += sunlight.color * sunlight.strength * (diffuse + specular + sunlight.ambient);
  }

  pixel *= light;

  imageStore(albedo, ivec2(gl_GlobalInvocationID.xy), vec4(pixel, 1.0));
}
